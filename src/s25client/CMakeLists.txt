# Add Icon to Application
if(WIN32)
    if(CMAKE_HOST_WIN32)
        SET(s25client_RC "${CMAKE_SOURCE_DIR}/win32/s25client.rc")
        if(MSVC)
            ADD_FLAGS(CMAKE_EXE_LINKER_FLAGS /MANIFEST:NO)
        endif()
    else()
        ADD_DEFINITIONS("-DAFX_TARG_DEU")
        ADD_FLAGS(CMAKE_EXE_LINKER_FLAGS -Wl,--stack,8388608)

        ADD_CUSTOM_COMMAND(OUTPUT ${CMAKE_CURRENT_BINARY_DIR}/s25client.res.o
                           COMMAND ${CMAKE_RC_COMPILER} -I${CMAKE_SOURCE_DIR}/win32/ -i${CMAKE_SOURCE_DIR}/win32/s25client.rc -o ${CMAKE_CURRENT_BINARY_DIR}/s25client.res.o)

        SET(s25client_RC ${CMAKE_CURRENT_BINARY_DIR}/s25client.res.o)
    endif()
ELSE()
	SET(s25client_RC "")
ENDIF()

ADD_EXECUTABLE(s25client s25client.cpp ${s25client_RC})
TARGET_LINK_LIBRARIES(s25client s25Main)

if("${CMAKE_SYSTEM_NAME}" STREQUAL "Darwin")
	TARGET_LINK_LIBRARIES(s25client ${SDL_LIBRARY} SDLmain)
	SET_TARGET_PROPERTIES(s25client PROPERTIES LINK_FLAGS "-framework Cocoa")
elseif(WIN32)
	TARGET_LINK_LIBRARIES(s25client ole32 ws2_32 shlwapi imagehlp)
elseif("${CMAKE_SYSTEM_NAME}" STREQUAL "Linux")
	TARGET_LINK_LIBRARIES(s25client pthread)
elseif("${CMAKE_SYSTEM_NAME}" STREQUAL "FreeBSD")
	TARGET_LINK_LIBRARIES(s25client execinfo)
ENDif()

if(MSVC)
	CMAKE_POLICY(SET CMP0026 OLD) # Required for use of LOCATION_*
	INCLUDE(CreateLaunchers)
	CREATE_DEFAULT_TARGET_LAUNCHER(s25client ENVIRONMENT RTTR_PREFIX_DIR=${CMAKE_BINARY_DIR})
ELSE()
	SET_TARGET_PROPERTIES(s25client PROPERTIES
		RUNTIME_OUTPUT_DIRECTORY_RELEASE "${CMAKE_BINARY_DIR}/${RTTR_BINDIR}"
		RUNTIME_OUTPUT_DIRECTORY_DEBUG "${CMAKE_BINARY_DIR}/${RTTR_BINDIR}"
		RUNTIME_OUTPUT_DIRECTORY_RELWITHDEBINFO "${CMAKE_BINARY_DIR}/${RTTR_BINDIR}"
	)
ENDIF()

if(WIN32)
    include(GatherDll)
    gather_dll_copy(s25client)
endif()

INSTALL(TARGETS s25client RUNTIME DESTINATION ${RTTR_BINDIR})
