# Add Icon to Application
if(WIN32)
    set(rcDir ${PROJECT_SOURCE_DIR}/data/win32)
    if(CMAKE_HOST_WIN32)
        set(s25client_RC ${rcDir}/s25client.rc)
    else()
        add_definitions(-DAFX_TARG_DEU)
        set(s25client_RC ${CMAKE_CURRENT_BINARY_DIR}/s25client.res.o)
        ADD_CUSTOM_COMMAND(OUTPUT ${s25client_RC}
                           COMMAND ${CMAKE_RC_COMPILER} -I${rcDir}/ -i${rcDir}/s25client.rc -o ${s25client_RC})

    endif()
ELSE()
    set(s25client_RC )
ENDIF()

find_package(Boost REQUIRED program_options)

add_executable(s25client s25client.cpp ${s25client_RC})
target_link_libraries(s25client PRIVATE s25Main Boost::program_options nowide::static)

if(APPLE)
    set(SDL_BUILDING_LIBRARY OFF)
    find_package(SDL REQUIRED)
	target_include_directories(s25client SYSTEM PRIVATE ${SDL_INCLUDE_DIR})
    if(NOT SDLMAIN_LIBRARY)
        set(SDLMAIN_LIBRARY ${PROJECT_SOURCE_DIR}/external/macos/libSDLmain.a)
    endif()
    target_link_libraries(s25client PRIVATE ${SDL_LIBRARY} ${SDLMAIN_LIBRARY})
elseif(WIN32)
    target_include_directories(s25client PRIVATE ${rcDir})
	target_link_libraries(s25client PRIVATE ole32 ws2_32 shlwapi imagehlp)
    if(MSVC)
        set_target_properties(s25client PROPERTIES LINK_FLAGS /MANIFEST:NO)
    endif()
    if(CMAKE_COMPILER_IS_GNUCXX)
        set_target_properties(s25client PROPERTIES LINK_FLAGS -Wl,--stack,8388608)
    endif()
elseif(CMAKE_SYSTEM_NAME STREQUAL "Linux")
	target_link_libraries(s25client PRIVATE pthread)
elseif(CMAKE_SYSTEM_NAME STREQUAL "FreeBSD")
	target_link_libraries(s25client PRIVATE execinfo)
ENDif()

if(MSVC)
	set_property(DIRECTORY ${CMAKE_SOURCE_DIR} PROPERTY VS_STARTUP_PROJECT s25client)
ENDIF()

if(WIN32)
    include(GatherDll)
    gather_dll_copy(s25client)
endif()

INSTALL(TARGETS s25client RUNTIME DESTINATION ${RTTR_BINDIR})
